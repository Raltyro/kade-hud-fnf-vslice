/**
 * Kade Engine HUD in FNF V-Slice Module By Raltyro
 * Trying my best to like document and explain whatever is going on in this module.
 * 
 * First made on 2 May 2024 Alongside with Nebula Zorua's COOLHUD V-Slice.
 * Second update at 7 June 2025.
 * Third Update at 22 July 2025. (For to fix the FlxSave blacklist.)
 * FNF V-Slice (0.3.0, WeekEnd1 Update) was first publicly released on 1 May 2024,
 *  then it's source code released publicly at GitHub FunkinCrew, previously ninjamuffin99 Github's
 *  on May 6 2024.
 *  That time no documentation was available, only digging into Web HTML5 Build of the WeekEnd1 Update
 *  Funkin.js.
 *  Crowplexus & Nebula Zorua's was the first modders who figured out all of the polymod shit.
 * 
 * This was the only available documentation to FNF V-Slice Modding at 2 May 2024
 * https://github.com/crowplexus/Funkin-VSlice-Template/blob/master/Template_Module.hxc
 * 
 * Nowadays you can always check FunkinCrew/Funkin source code.
 * https://github.com/FunkinCrew/Funkin
 */

import funkin.graphics.FunkinSprite;
import funkin.modding.module.Module;
import funkin.save.Save;
import funkin.play.notes.notestyle.NoteStyle;
import funkin.play.notes.notestyle.ScriptedNoteStyle;
import funkin.play.scoring.Scoring;
import funkin.play.PlayState;
import funkin.ui.options.OptionsState;
import funkin.ui.options.OptionsMenu;
import funkin.ui.options.PreferencesMenu;
import funkin.ui.mainmenu.MainMenuState;
import funkin.util.Constants;
import funkin.Conductor;
import funkin.FunkinMemory;
import funkin.Highscore;
import funkin.Paths;
import funkin.Preferences;

import flixel.math.FlxMath;
import flixel.text.FlxTextBorderStyle;
import flixel.text.FlxText;
import flixel.tweens.FlxEase;
import flixel.tweens.FlxTween;
import flixel.ui.FlxBar;
import flixel.util.FlxSpriteUtil;
import flixel.util.FlxStringUtil;
import flixel.FlxBasic;
import flixel.FlxG;
import flixel.FlxState;
import flixel.FlxSprite;

// The main Module for this HUD Mod.
class KadeHUDModule extends Module {
	// Module & Saving Implementation in funkin.modding.IScriptedClass

	/**
	 * The module constructor is only (ModuleID, Priority)
	 * ModuleID has to be different with the other activated mods modules.
	 * Priority is the order runtime for this module. Higher being the first module to run.
	 */
	public function new() {
		super("KadeHUD", 1);
		initSave();
	}

	/**
	 * It used to use FlxSave until v0.7.0, it got blacklisted because of a bypass on doing resolveClasses.
	 */
	var saveData:Dynamic;

	private function initSave() {
		saveData = Save.instance.getModOptions("kadehudvslice");

		// default
		if (saveData.enabled == null) saveData.enabled = true;
		if (saveData.accHoldToAcc == null) saveData.accHoldToAcc = true;
		if (saveData.simpleAcc == null) saveData.simpleAcc = false;
		if (saveData.showNPS == null) saveData.showNPS = false;
		if (saveData.showTapMisses == null) saveData.showTapMisses = true;
		if (saveData.showTimer == null) saveData.showTimer = true;
		if (saveData.showSongInfo == null) saveData.showSongInfo = true;
		if (saveData.showTiming == null) saveData.showTiming = false;
		if (saveData.centerStrums == null) saveData.centerStrums = true;
		if (saveData.perfectRating == null) saveData.perfectRating = true;
	}

	/**
	 * The destroy callback for modules.
	 * Though weird behavior of this being called in PlayState too instead of you know, being called
	 *  seperately like onPlayStateDestroy.
	 * 
	 * This is fucked.
	 */
	override function onDestroy(e:ScriptEvent) {
		cleanup();
	}

	/**
	 * Build a this HUD Mod Preferences Menu for OptionsState.
	 * You cannot extend PreferencesMenu, so we have to make it then modify it after.
	 */
	private function buildModPreferencesMenu():PreferencesMenu {
		var menu:PreferencesMenu = new PreferencesMenu();
		initSave();

		// Destroy a pre-built Preferences Menu then make our own.
		for (item in menu.items) item.destroy();
		for (item in menu.preferenceItems) item.destroy();
		menu.items.clear();
		menu.preferenceItems.clear();
		menu.preferenceDesc = [];

		menu.createPrefItemCheckbox("Enable Kade HUD", "Enables this Kade Engine HUD Mod.",
			(value) -> saveData.enabled = value, saveData.enabled);

		menu.createPrefItemCheckbox("Show Perfect Rating", "If enabled, Perfect Timing will be shown seperately as a judgement.",
			(value) -> saveData.perfectRating = value, saveData.perfectRating);

		menu.createPrefItemCheckbox("Simple Accuracy", "Changes how the accuracy will display depending on how simple it is on this setting.",
			(value) -> saveData.simpleAcc = value, saveData.simpleAcc);

		menu.createPrefItemCheckbox("Account Hold to Acc", "If enabled, any hold will be accounted to accuracy in the score label.",
			(value) -> saveData.accHoldToAcc = value, saveData.accHoldToAcc);

		menu.createPrefItemCheckbox("Diff Timing Text", "If enabled, a text will popup on note indicating a timing differences.",
			(value) -> saveData.showTiming = value, saveData.showTiming);

		menu.createPrefItemCheckbox("Notes per Second", "If enabled, this'll shows text indicating NPS in the score label.",
			(value) -> saveData.showNPS = value, saveData.showNPS);

		menu.createPrefItemCheckbox("Tap Misses", "If enabled, this'll show text indicating How much tap misses in the score label.",
			(value) -> saveData.showTapMisses = value, saveData.showTapMisses);

		menu.createPrefItemCheckbox("Timer Bar", "If enabled, this'll show a timer bar on top of the screen.",
			(value) -> saveData.showTimer = value, saveData.showTimer);

		menu.createPrefItemCheckbox("Song Info Counter", "If enabled, this'll show information text about the current song on the left bottom corner of the screen.",
			(value) -> saveData.showSongInfo = value, saveData.showSongInfo);

		menu.createPrefItemCheckbox("Center Strumlines", "If enabled, this'll center the strums like in Kade Engine.",
			(value) -> saveData.centerStrums = value, saveData.centerStrums);

		menu.itemDesc.text = menu.preferenceDesc[menu.items.selectedIndex];

		return menu;
	}

	/**
	 * A helper function to easily add custom option items to OptionsMenu.
	 * Dynamic in customItems is {index:Int, label:String, callback:Void->Void, ?fireInstantly:Bool}
	 */
	private function addCustomOptionItems(options:OptionsMenu, customItems:Array<Dynamic>) {
		// Try to atleast fix the OptionsMenu first.
		if (!options.hasMultipleOptions()) {
			trace("this OptionsMenu's is weird! trying to atleast fix it somehow.");
			options.codex.setPage("options");

			var exitToMainMenuCallback = currentState.exitToMainMenu;
			if (exitToMainMenuCallback != null) exitToMainMenuCallback = () -> {
				options.codex.currentPage.enabled = false;
				FlxG.switchState(() -> new MainMenuState());
			};
			options.onExit.add(exitToMainMenuCallback);

			if (options.items.getItem("EXIT") == null) customItems.push({index: 999999, label: "EXIT", callback: options.exit});
		}

		// They fucked up the hscript parser somehow, so doing items.push(item = createItem) just creates the item thrice.
		var items = [], requestIndices:Map<Int, Array<Int>> = [-1 => []], length = options.items.length + customItems.length, id = 0;
		for (dyn in customItems) {
			var item = options.createItem(dyn.label, dyn.callback, dyn.fireInstantly);
			items.push(item);
			options.items.remove(item, true);

			var reqIdx = (dyn.index > length) ? length : dyn.index;
			if (!requestIndices.exists(reqIdx = (reqIdx > 0) ? reqIdx : 0)) requestIndices.set(reqIdx, [id]);
			else requestIndices.get(reqIdx).push(id);
			id++;
		}

		var members = options.items.members, y = (FlxG.height - length * 100) * 0.5 - 90;
		function checkRequestIndices() if (requestIndices.exists(id)) for (itemID in requestIndices.get(id)) {
			var item = items[itemID];
			item.y = y += 100;

			if (id == options.items.selectedIndex) {
				item.select();
				if (members[id] != null) members[id].idle();
			}

			options.items.insert(id, item);
			id++;
			checkRequestIndices();
		}

		id = 0;
		while (id < length) {
			checkRequestIndices();
			var item = members[id];
			if (item != null) item.y = y += 100;
			id++;
		}
	}

	// Options & PlayState handling in funkin.modding.IScriptedClass.IStateChangingScriptedClass

	/**
	 * This is where to store the current FlxState to access PlayState or any other states, and
	 *  handling the currentState modification in runtime.
	 * 
	 * In HScript, you can dynamically access a variable like currentState to PlayState exclusive variables.
	 * whereas in normal Haxe, you would have to cast it everytime like cast(currentState, PlayState)
	 */
	var currentState:FlxState;
	var inPlay:Bool = false;

	override function onStateChangeBegin(e:StateChangeScriptEvent) {
		inPlay = false;
		cleanup();
	}

	override function onStateChangeEnd(e:StateChangeScriptEvent) {
		currentState = e.targetState;

		// Use Std.isOfType to check if this is PlayState or OptionsState.
		if (Std.isOfType(currentState, PlayState)) onPlaystateEnter();
		else if (Std.isOfType(currentState, OptionsState)) {
			var options = currentState.optionsCodex.pages.get("options");
			var modPreferences = currentState.optionsCodex.addPage("kadehud", buildModPreferencesMenu());
			modPreferences.onExit.add(() -> {
				modPreferences.codex.switchPage("options");
				Save.system.flush();
			});

			addCustomOptionItems(options, [
				{index: options.items.members.indexOf(options.items.getItem("PREFERENCES")) + 1,
					label: "KADE HUD", callback: () -> options.codex.switchPage("kadehud")}
			]);
		}
	}

	// funkin.modding.IScriptedClass.IPlayStateScriptedClass
	/**
	 * Just a disclaimer, because Modules starts first when events being dispatched, it's not sure if
	 *  an event has been cancelled by other scripts after that.
	 * 
	 * So that's... fun...
	 */

	/**
	 * These should be the variables for PlayState.
	 * Define cleanup for to call/reuse the variables on onDestroy, onPlayStateEnter, onStateChangeEnd
	 *  (just incase), this is for to avoid memory leaks.
	 */
	var scoreText:FlxText;
	var timingText:FlxText;
	var kadeText:FlxText;
	var songPosOverlay:FunkinSprite;
	var songPosBar:FlxBar;
	var songPosText:FlxText;

	private function cleanup() {
		if (scoreText != null) scoreText.destroy();
		if (timingText != null) timingText.destroy();
		if (kadeText != null) kadeText.destroy();
		if (songPosOverlay != null) songPosOverlay.destroy();
		if (songPosBar != null) songPosBar.destroy();
		if (songPosText != null) songPosText.destroy();
	}

	/**
	 * A Seperate statistics/tallies variables for PlayState.
	 * prevCombos - for to check combo breaks.
	 * maxScore, score - for accuracy, it requires a seperate score variable instead of PlayState.songScore
	 *  for Preferences like Account Hold to Accuracies, Simple Accuracy.
	 * 
	 * lastSongPos - for maxScore Holds (in somewhere timeline where FNF doesn't ties up Hold Bonus Score to
	 *  delta time)
	 * 
	 * lastScore, lastMaxScore - for detect any changes to score to update Score Text, it needs to make sure
	 *  to not causes any more changes to text or waste computing time on the same tallies.
	 * 
	 * nps - for NPS, obviously, it acts like a timer inserting itself from 1 then decrementing by delta
	 */
	var comboBreaks:Int;
	var tapMissed:Int;

	var lastSongPos:Float;
	var lastTimeLeft:Int = 0;
	var lastScore:Int = 0;
	var lastMaxScore:Int = 0;
	var nps:Array<Float> = [];

	var prevCombos:Int;
	var maxScore:Int;
	var score:Int;
	var timeLeft:Int;

	private function resetStats() {
		comboBreaks = 0;
		tapMissed = 0;

		lastSongPos = 0;
		nps.resize(0);

		prevCombos = 0;
		maxScore = 0;
		score = 0;
	}

	/**
	 * This is where it implant the mod Kade HUD to PlayState.
	 * More explanation at the codes.
	 */
	private function onPlaystateEnter() {
		inPlay = true;

		cleanup();
		if (!saveData.enabled) return;

		resetStats();

		// I imagine there would be competitive players using this shitty mod, so i'd like to cache the judgments to not cause any lag spikes.
		for (rating in ["sick", "good", "bad", "shit"]) FunkinMemory.cacheTexture(currentState.comboPopUps.noteStyle.buildJudgementSpritePath(rating));
		if (saveData.perfectRating) FunkinMemory.cacheTexture(currentState.comboPopUps.noteStyle.buildJudgementSpritePath("sick") + "2");

		if (saveData.centerStrums) {
			currentState.playerStrumline.x = FlxG.width * 0.75 - currentState.playerStrumline.width * 0.5;
			currentState.opponentStrumline.x = FlxG.width * 0.25 -  currentState.opponentStrumline.width * 0.5;
		}

		// Re-use the available scoreText on PlayState instead of making one, then make a temporary a really small FlxText to PlayState.scoreText.
		// To not cause any crashes relating to Score Text not existing/memory leaks being rapidly changes.
		// (Which kinda defeats the entire purpose?)
		scoreText = currentState.scoreText;
		currentState.scoreText = new FlxText(0, 0, 1000);
		currentState.scoreText.visible = false;
		currentState.scoreText.active = false;

		changeScoreText();

		if (saveData.showSongInfo) {
			currentState.add(kadeText = new FlxText(18, FlxG.height - 50, 0, currentState.currentChart.songArtist + "\n" + currentState.currentChart.songName + " - " + FlxStringUtil.toTitleCase(currentState.currentDifficulty), 20));
			kadeText.setFormat(Paths.font("vcr.ttf"), 16, -1, "left", FlxTextBorderStyle.OUTLINE_FAST, -16777216);
			kadeText.zIndex = 802;
			kadeText.cameras = [currentState.camHUD];
		}

		// Kinda stolen from Boloxd's Kade Timing mod, i thought just implement it to just one mod.
		// And make the implementation much accurate.
		if (saveData.showTiming) {
			currentState.add(timingText = new FlxText(0, 0, 120, "").setFormat(Paths.font("pixel.otf"), 18, 0, "center", FlxTextBorderStyle.OUTLINE_FAST));
			timingText.borderColor = 0xFF000000;
			timingText.borderSize = 2;
			timingText.zIndex = 801;
			timingText.cameras = [currentState.camHUD];
		}

		if (saveData.showTimer) {
			// I like to have the overlay image, that then it won't have pixelated artifacts sort of thing, full colored textures makes it crusty on the edges.
			songPosOverlay = FunkinSprite.create(0, 0, "timeBar");
			songPosOverlay.setPosition(Math.floor((FlxG.width - songPosOverlay.width) * 0.5), Preferences.downscroll ? FlxG.height - 17 - songPosOverlay.height : 17);
			songPosOverlay.zIndex = 802;

			songPosBar = new FlxBar(songPosOverlay.x + 2, songPosOverlay.y + 2, null, songPosOverlay.width - 4, 1);
			songPosBar.createGradientBar([0xAF000000], [0xFF82EBCF, 0xFFF4FF9B]);
			songPosBar.scale.y = songPosOverlay.height - 4;
			songPosBar.numDivisions = Math.min(songPosBar.width, 1024);
			songPosBar.zIndex = 801;
			songPosBar.updateHitbox();

			songPosText = new FlxText(songPosOverlay.x, Math.floor(songPosOverlay.y + (songPosBar.height - 15) * 0.5), songPosOverlay.width, "", 20);
			songPosText.setFormat(Paths.font("vcr.ttf"), 20, -1, "center", FlxTextBorderStyle.OUTLINE_FAST, -16777216);
			songPosText.zIndex = 803;
			songPosText.borderSize = 2;

			for (member in [songPosText, songPosBar, songPosOverlay]) {
				member.alpha = 0;
				member.cameras = [currentState.camHUD];
			}

			if (tweenSongPosNext) {
				tweenSongPos();
				tweenSongPosNext = false;
			}
		}
		else
			tweenSongPosNext = false;
	}

	// Not sure what was this for exactly, this just existed way back in the old version of this mod.
	var tweenSongPosNext:Bool = false;
	private function tweenSongPos() {
		for (member in [songPosBar, songPosOverlay, songPosText]) {
			currentState.add(member);
			FlxTween.tween(member, {alpha: 1}, 1, {ease: FlxEase.circOut});
		}
	}

	/**
	 * From this on, i'm lazy to explain stuff.
	 * It's should be self-explanatory.
	 * 
	 * If you want more explanation, head back to FNF Source Code
	 * funkin.modding.events.ScriptEvent.
	 * All of the implemented script event callbacks should be there.
	 */

	/**
	 * e.difficulty
	 */
	override function onSongRetry(e:SongRetryEvent) {
		if (/*e.eventCanceled ||*/ !saveData.enabled) return;
		resetStats();
	}

	/**
	 * e.step
	 */
	override function onCountdownEnd(e:CountdownScriptEvent) {
		if (/*e.eventCanceled ||*/ !saveData.enabled) return;
		if (!inPlay) tweenSongPosNext = true;
		else if (saveData.showTimer) tweenSongPos();
	}

	var doCheckNoteStyleTemp:Bool = false;
	var tempNoteStyleFallback:String;
	var tempNoteStyleSickPath:String;
	var tempNoteStyleSickData:Dynamic;
	private function checkNoteStyleTemp() if (doCheckNoteStyleTemp) {
		currentState.comboPopUps.noteStyle._data.fallback = tempNoteStyleFallback;
		currentState.comboPopUps.noteStyle._data.assets.judgementSick = tempNoteStyleSickData;
		tempNoteStyleSickData.assetPath = tempNoteStyleSickPath;
		doCheckNoteStyleTemp = false;
	}

	/**
	 * e.judgement
	 * e.score
	 * e.isComboBreak
	 * e.hitDiff
	 * e.doesNotesplash
	 * 
	 * NoteScriptEvent
	 * e.note
	 * e.comboCount
	 * e.playSound
	 * e.healthChange
	 */
	override function onNoteHit(e:HitNoteScriptEvent) {
		// they broke shit in the hscript parser so getMustHitNote is just broke
		if (/*e.eventCanceled ||*/ !inPlay || !saveData.enabled || e.note.noteData.data >= 4) return;

		nps.push(1); // Adds a variable starting from 1 to nps array to indicate that a note has been hit.
		maxScore += Scoring.PBOT1_MAX_SCORE; // Add the max score on note hit.

		// If any configurations isn't required to pass any of these statements, Then it's not using a custom score to calculate the accuracy.
		if (saveData.simpleAcc) {
			// If it's using Simple Accuracy configuration, round the added score to the custom score.
			switch (e.judgement) {
				case 'sick': score += Scoring.PBOT1_MAX_SCORE;
				case 'good': score += Scoring.scoreNotePBOT1(Scoring.PBOT1_SICK_THRESHOLD);
				case 'bad': score += Scoring.scoreNotePBOT1(Scoring.PBOT1_GOOD_THRESHOLD);
				case 'shit': score += Scoring.scoreNotePBOT1(Scoring.PBOT1_BAD_THRESHOLD);
			}
		}
		else if (saveData.accHoldToAcc)
			score = currentState.songScore;
		else // If the current configuration do not account holds to accuracy, use the custom score instead.
			score += e.score;

		// AGHH i don't like how to do this cuz like setting e.judgement is nono and makes gf to play drop down combo anim
		if (saveData.perfectRating && e.score == Scoring.PBOT1_MAX_SCORE && e.judgement == "sick") {
			var noteStyleData = currentState.comboPopUps.noteStyle._data;
			if (doCheckNoteStyleTemp) {
				noteStyleData.assets.judgementSick = tempNoteStyleSickData;
				noteStyleData.fallback = tempNoteStyleFallback;
			}
			else {
				tempNoteStyleSickData = noteStyleData.assets.judgementSick;
				doCheckNoteStyleTemp = tempNoteStyleSickData != null;
				if (doCheckNoteStyleTemp) {
					tempNoteStyleSickPath = tempNoteStyleSickData.assetPath;
					tempNoteStyleFallback = noteStyleData.fallback;
					tempNoteStyleSickData.assetPath += "2";
				}
			}

			currentState.comboPopUps.displayRating("sick");

			noteStyleData.assets.judgementSick = null;
			noteStyleData.fallback = null;
		}
		else
			checkNoteStyleTemp();

		if (saveData.showTiming) {
			// Show the timing difference label if a note has hit.
			timingText.setPosition(FlxG.width * 0.474 + 50 + currentState.comboPopUps.offsets[0], FlxG.height * 0.45 - 20 + currentState.comboPopUps.offsets[1]);
			timingText.text = FlxMath.roundDecimal(e.hitDiff, 2) + "ms";
			timingText.alpha = 1.0;

			switch (e.judgement) {
				case "good": timingText.color = 0x00FF00;
				case "sick": timingText.color = 0x00FFFF;
				default: timingText.color = 0xFF0000;
			}

			FlxTween.cancelTweensOf(timingText);
			FlxTween.tween(timingText, {alpha: 0}, 0.2, {startDelay: 0.1});
		}

		updateScoreText();
	}

	/**
	 * e.note
	 * e.comboCount
	 * e.playSound
	 * e.healthChange
	 */
	override function onNoteMiss(e:NoteScriptEvent) {
		// they broke shit in the hscript parser so getMustHitNote is just broke
		if (/*e.eventCanceled ||*/ !inPlay || !saveData.enabled || e.note.noteData.data >= 4) return;

		maxScore += Scoring.PBOT1_MAX_SCORE;
		score -= 10; // this one is hardcoded...
		prevCombos = Highscore.tallies.combo;
		comboBreaks += 1;

		if (saveData.simpleAcc || !saveData.accHoldToAcc) updateScoreText();
	}


	/**
	 * e.dir
	 * e.hasPossibleNotes
	 * e.healthChange
	 * e.scoreChange
	 * e.playSound
	 * e.playAnim
	 */
	override function onNoteGhostMiss(e:GhostMissNoteScriptEvent) {
		if (/*e.eventCanceled ||*/ !inPlay || !saveData.enabled) return;

		score += e.scoreChange;
		tapMissed += 1;

		updateScoreText();
	}

	private function updateScoreText() {
		if (lastScore != score || lastMaxScore != maxScore) changeScoreText();

		lastScore = score;
		lastMaxScore = maxScore;
	}

	/**
	 * e.elapsed
	 */
	override function onUpdate(e:UpdateScriptEvent) {
		if (!inPlay || !saveData.enabled) return;

		if (prevCombos > 0 && Highscore.tallies.combo <= Math.min(prevCombos, 0)) comboBreaks += 1;
		prevCombos = Highscore.tallies.combo;

		var songPos = Conductor.instance.songPosition, elapsed = e.elapsed;

		if (saveData.showNPS) {
			var i = nps.length;
			while (i > 0) { // FlxArrayUtil.swapAndPop is inlined
				i--;
				nps[i] -= elapsed;
				if (nps[i] < 0) {
					nps[i] = nps[nps.length - 1];
					nps.pop();
				}
			}
		}

		if (saveData.accHoldToAcc) {
			if (!saveData.simpleAcc) score = currentState.songScore;
			if (songPos > lastSongPos && currentState.playerStrumline != null) {
				var addScore:Int;
				for (hold in currentState.playerStrumline.holdNotes.group.members) if (hold != null && hold.alive && songPos > hold.strumTime) {
					addScore = songPos < hold.noteData.length + hold.strumTime ? Math.floor(Constants.SCORE_HOLD_BONUS_PER_SECOND * elapsed) : 0;
					// Implement this instead whenever they fixed delta-time specified only hold bonus scores.
					//addScore = Constants.SCORE_HOLD_BONUS_PER_SECOND * 0.001 * Math.min(songPos - lastSongPos, Math.max(hold.noteData.length - hold.strumTime, 0));

					maxScore += addScore;
					if (saveData.simpleAcc && hold.hitNote && !hold.missedNote) score += addScore;
				}
			}
		}

		if (saveData.showTimer) {
			if ((timeLeft = Math.floor((currentState.currentSongLengthMs - songPos) * 0.001)) != lastTimeLeft)
				songPosText.text = currentState.currentChart.songName + " (" + FlxStringUtil.formatTime(timeLeft, false) + ")";

			songPosBar.value = (songPos / currentState.currentSongLengthMs) * 100;
			songPosBar.updateBar();

			lastTimeLeft = timeLeft;
		}

		checkNoteStyleTemp();
		updateScoreText();

		lastSongPos = songPos;
	}

	private function changeScoreText() {
		//var tallies = Highscore.tallies;
		//var notes = tallies.sick + (tallies.good * 0.785) + (tallies.bad * 0.65) + (tallies.shit * 0.3);
		// (notes / (tallies.totalNotesHit + tallies.missed)
		var acc = maxScore < 1 ? 0 : (score / maxScore) * 100;
		var text = "Score: " + currentState.songScore;

		if (saveData.showNPS) text = "NPS: " + nps.length + " | " + text;

		if (saveData.showTapMisses && tapMissed != 0) text += " | (Tap Misses: " + tapMissed + ") Breaks: " + comboBreaks;
		else text += " | Combo Breaks: " + (comboBreaks + tapMissed);

		text += " | Accuracy: " + FlxMath.roundDecimal(acc, 2) + "% | " + getRating(acc);

		scoreText.text = text;
		scoreText.setPosition(currentState.healthBar.x + (currentState.healthBar.width - scoreText.width) * 0.5, currentState.healthBar.y + 33);
	}

	// Stolen from Kade Engine probably? not sure this is old code.
	var wifeConditions:Array<Array<Dynamic>> = [
		[99.9935, "AAAAA"],
		[99.980, "AAAA:"],
		[99.970, "AAAA."],
		[99.955, "AAAA"],
		[99.90, "AAA:"],
		[99.80, "AAA."],
		[99.70, "AAA"],
		[99, "AA:"],
		[96.50, "AA."],
		[93, "AA"],
		[90, "A:"],
		[85, "A."],
		[80, "A"],
		[70, "B"],
		[60, "C"]
	];

	private function getRating(acc:Float):String {
		var misses = tapMissed + comboBreaks, fc:String;
		if (misses > 0) {
			if (misses < 10) fc = "SDCB";
			else fc = "Clear";
		}
		else {
			if (Highscore.tallies.bad > 0 || Highscore.tallies.shits > 0) fc = "FC";
			else if (Highscore.tallies.good > 0) fc = "GFC";
			else if (Highscore.tallies.sick > 0) fc = "SFC";
			else fc = "N/A";
		}

		for (b in wifeConditions) if (b[0] < acc) return "(" + fc + ") " + b[1];
		return "(" + fc + ") D";
	}
}